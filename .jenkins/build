#!/usr/bin/env groovy
// Copyright (C) 2021 Edge Network Technologies Limited
// Use of this source code is governed by a GNU GPL-style license
// that can be found in the LICENSE.md file. All rights reserved.

@Library('semver-compare-lib@1.0.0') _

def buildAndPushImage(registry, registryCredential, tag, buildArgs, context) {
  docker.withRegistry("https://$registry", registryCredential) {
    def image = docker.build("$registry/$tag", "$buildArgs $context")
    image.push()
    return "$registry/$tag"
  }
}

def b64e(input) {
  return input.bytes.encodeBase64().toString()
}

def deployScript() {
  return b64e('''
  #!/bin/bash

  # Pull the latest version
  docker pull registry.edge.network/internal/cli-x64
  docker pull registry.edge.network/internal/cli-arm64

  # Transfer x64 binaries from image to volume
  docker run \
    --rm \
    --env NETWORK=$NETWORK \
    --env VERSION=$VERSION \
    --volume fileserver-data:/mnt/fileserver \
    $REGISTRY/internal/cli-x64

  # Transfer arm64 binaries from image to volume
  docker run \
    --rm \
    --env NETWORK=$NETWORK \
    --env VERSION=$VERSION \
    --volume fileserver-data:/mnt/fileserver \
    $REGISTRY/internal/cli-arm64
  ''')
}

def skipRemainingStages = false
def version

pipeline {
  agent {
    label 'remote'
  }
  stages {
    stage('Check version') {
      steps {
        script {
          def versionCheckUrl = 'https://files.edge.network/cli/$NETWORK/linux/x64/latest/version'
          def latestVersion = sh(script: "curl -s $versionCheckUrl", returnStdout: true).trim()
          version = sh(script: 'cat package.json | jq -r .version', returnStdout: true).trim()

          if (semver_compare(version, latestVersion) <= 0 && FORCE_UPDATE == 'false') {
            skipRemainingStages = true
            println "version $version <= latest version $latestVersion, skipping"
          }
        }
      }
    }

    stage('Build') {
      failFast true
      when {
        expression {
          !skipRemainingStages
        }
      }
      parallel {
        stage('x64') {
          agent {
            label 'arch-x64'
          }
          steps {
            script {
              def buildArgs = "--build-arg NETWORK=$NETWORK --build-arg NODE=$NODE --build-arg ARCH=x64"
              buildAndPushImage(REGISTRY, REGISTRY_CREDENTIAL, "internal/cli-x64", buildArgs, ".jenkins/build")
            }
          }
        }
        stage('arm64') {
          agent {
            label 'arch-arm64'
          }
          steps {
            script {
              def buildArgs = "--build-arg NETWORK=$NETWORK --build-arg NODE=$NODE --build-arg ARCH=arm64"
              buildAndPushImage(REGISTRY, REGISTRY_CREDENTIAL, "internal/cli-arm64", buildArgs, ".jenkins/build")
            }
          }
        }
      }
    }

    stage('Package') {
      agent {
        label 'arch-arm64'
      }
      when {
        expression {
          !skipRemainingStages
        }
      }
      steps {
        script {
          def buildArgs = "--build-arg NETWORK=$NETWORK --build-arg NODE=$NODE"
          buildAndPushImage(REGISTRY, REGISTRY_CREDENTIAL, "internal/cli-packaged", buildArgs, ".jenkins/package")
        }
      }
    }

    stage('Deploy') {
      when {
        expression {
          !skipRemainingStages
        }
      }
      steps {
        script {
          def sshPort = env.SSH_PORT != null ? env.SSH_PORT : 22
          def ssh = "ssh -o ServerAliveInterval=120 -o ServerAliveCountMax=5 -o StrictHostKeyChecking=no -p ${sshPort} jenkins@${DEPLOY_TARGET} "
          def filename = "deploy_${UUID.randomUUID().toString()}.sh"
          def script = deployScript()

          sshagent(credentials: ['a687b920-fcb4-41e4-a878-5ae1f6850b26']) {
            println "> deploying CLI binaries to ${DEPLOY_TARGET}..."

            // Execute the script
            sh "$ssh 'echo $script | base64 -d > ${filename} && \
              chmod +x ${filename} && \
              REGISTRY=$REGISTRY \
              NETWORK=$NETWORK \
              VERSION=$version \
                bash -x ./${filename} && \
              rm ${filename}'"
          }
        }
      }
    }
  }
}
