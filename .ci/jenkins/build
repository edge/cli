#!/usr/bin/env groovy
// Copyright (C) 2021 Edge Network Technologies Limited
// Use of this source code is governed by a GNU GPL-style license
// that can be found in the LICENSE.md file. All rights reserved.

@Library('semver-compare-lib@1.0.0') _

def buildAndPushImage(registryCredential, tag, args) {
  docker.withRegistry("https://registry.edge.network", registryCredential) {
    def image = docker.build("registry.edge.network/$tag", "$args .")
    image.push()
    return "registry.edge.network/$tag"
  }
}

def b64e(input) {
  return input.bytes.encodeBase64().toString()
}

def deployScript() {
  return b64e('''
  #!/bin/bash

  # Pull the latest version
  docker pull registry.edge.network/internal/cli-packaged:$NETWORK

  # Transfer x64 binaries from image to volume
  docker run \
    --rm \
    --env NETWORK=$NETWORK \
    --env VERSION=$VERSION \
    --volume fileserver-data:/mnt/fileserver \
    registry.edge.network/internal/cli-packaged:$NETWORK
  ''')
}

def skipRemainingStages = false
def version

pipeline {
  agent {
    label 'local'
  }
  stages {
    stage('Check Version') {
      steps {
        script {
          def uname = sh(script: "uname -a", returnStdout: true).trim()
          println "[Check Version] Running on $uname"

          def versionCheckUrl = 'https://files.edge.network/cli/$NETWORK/linux/x64/latest/version'
          def latestVersion = sh(script: "curl -sk $versionCheckUrl", returnStdout: true).trim()
          println "Latest version: $latestVersion"

          version = sh(script: 'cat package.json | jq -r .version', returnStdout: true).trim()

          if (semver_compare(version, latestVersion) <= 0 && FORCE_UPDATE == 'false') {
            skipRemainingStages = true
            println "version $version <= latest version $latestVersion, skipping"
          }
        }
      }
    }

    stage('Build') {
      failFast true
      when {
        expression {
          !skipRemainingStages
        }
      }
      parallel {
        stage('x64') {
          agent {
            label 'arch-x64'
          }
          steps {
            script {
              def uname = sh(script: "uname -a", returnStdout: true).trim()
              println "[Build x64] Running on $uname"

              def args = "--file .ci/docker/build --build-arg NETWORK=$NETWORK --build-arg NODE=$NODE --build-arg ARCH=x64"
              buildAndPushImage(REGISTRY_CREDENTIAL, "internal/cli-x64:$NETWORK", args)
            }
          }
        }
        stage('arm64') {
          agent {
            label 'arch-arm64'
          }
          steps {
            script {
              def uname = sh(script: "uname -a", returnStdout: true).trim()
              println "[Build arm64] Running on $uname"

              def args = "--file .ci/docker/build --build-arg NETWORK=$NETWORK --build-arg NODE=$NODE --build-arg ARCH=arm64"
              buildAndPushImage(REGISTRY_CREDENTIAL, "internal/cli-arm64:$NETWORK", args)
            }
          }
        }
      }
    }

    stage('Package') {
      agent {
        label 'arch-x64'
      }
      when {
        expression {
          !skipRemainingStages
        }
      }
      steps {
        script {
          def uname = sh(script: "uname -a", returnStdout: true).trim()
          println "[Package] Running on $uname"

          // Ensure required images are up to date
          sh "docker pull registry.edge.network/internal/cli-x64:$NETWORK"
          sh "docker pull registry.edge.network/internal/cli-arm64:$NETWORK"

          def args = "--no-cache --file .ci/docker/package --build-arg NETWORK=$NETWORK --build-arg NODE=$NODE"
          buildAndPushImage(REGISTRY_CREDENTIAL, "internal/cli-packaged:$NETWORK", args)
        }
      }
    }

    stage('Deploy') {
      agent {
        label 'arch-x64'
      }
      when {
        expression {
          !skipRemainingStages
        }
      }
      steps {
        script {
          def uname = sh(script: "uname -a", returnStdout: true).trim()
          println "[Deploy] Running on $uname"

          def sshPort = env.SSH_PORT != null ? env.SSH_PORT : 22
          def ssh = "ssh -o ServerAliveInterval=120 -o ServerAliveCountMax=5 -o StrictHostKeyChecking=no -p ${sshPort} jenkins@${DEPLOY_TARGET} "
          def filename = "deploy_${UUID.randomUUID().toString()}.sh"
          def script = deployScript()

          sshagent(credentials: ['a687b920-fcb4-41e4-a878-5ae1f6850b26']) {
            println "> deploying CLI binaries to ${DEPLOY_TARGET}..."

            // Execute the script
            sh "$ssh 'echo $script | base64 -d > ${filename} && \
              chmod +x ${filename} && \
              NETWORK=$NETWORK \
              VERSION=$version \
                bash -x ./${filename} && \
              rm ${filename}'"
          }
        }
      }
    }
  }
  post {
    success {
      script {
        discordSend title: JOB_NAME, description: "${env.JOB_NAME} (#${env.BUILD_NUMBER}) has completed successfully", result: "SUCCESS", webhookURL: DISCORD_BUILD_WEBHOOK
      }
    }
    failure {
      script {
        discordSend title: JOB_NAME, description: "${env.JOB_NAME} (#${env.BUILD_NUMBER}) has failed", result: "FAILURE", webhookURL: DISCORD_BUILD_WEBHOOK
      }
    }
    aborted {
      script {
        discordSend title: JOB_NAME, description: "${env.JOB_NAME} (#${env.BUILD_NUMBER}) was aborted", result: "ABORTED", webhookURL: DISCORD_BUILD_WEBHOOK
      }
    }
  }
}
